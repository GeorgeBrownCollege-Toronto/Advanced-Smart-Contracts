<!doctype html>
<html>
	<head>
		<meta charset="utf-8">

		<title>Accessing storage, Exception handling and Debugging in Smart contract</title>

		<meta name="description" content="A lesson on various ways of contract interaction">
		<meta name="author" content="Dhruvin Parikh">

		<meta name="apple-mobile-web-app-capable" content="yes">
		<meta name="apple-mobile-web-app-status-bar-style" content="black-translucent">

		<meta name="viewport" content="width=device-width, initial-scale=1.0">

		<link rel="stylesheet" href="../../../reveal.js/dist/reset.css">
		<link rel="stylesheet" href="../../../reveal.js/dist/reveal.css">
		<link rel="stylesheet" href="../../../reveal.js/dist/theme/solarized.css" id="theme">

		<!-- Theme used for syntax highlighting of code -->
		<link rel="stylesheet" href="../../../reveal.js/plugin/highlight/monokai.css" id="highlight-theme">
		<style>
			.reveal .slides h1, .reveal .slides h2, .reveal .slides h3 {
        		text-transform: none;
     	 	}

      		.two-column {
        		display: flex;
        		flex-wrap: wrap;
      		}

      		.two-column em {
        		margin: 20px;
      		}

      		.reveal .big-and-bold {
        		font-weight: bold;
        		font-size: 135%;
      		}

      		.reveal .shrunk-a-bit {
        		font-size: 90%;
      		}

			.reveal .shrunk-a-bit pre {
       	 		width: 100%;
      		}

			.reveal pre {
        		width: 100%;
      		}

      		.reveal .highlight {
        		color: gray;
        		font-weight: bold;
      		}

      		.reveal .highlightRed {
        		color: red;
        		font-weight: bold;
      		}

      		.reveal .highlightGreen {
        		color: green;
        	font-weight: bold;
      		}
		</style>
	</head>
	<body>
		<div class="reveal">
			 <div class="slides">
				<section data-markdown>
					<textarea data-template>
						### BCDV 1013 - Advanced Smart Contracts 
						#### Accessing storage, Exception handling and Debugging 
						<div class="author">Dhruvin Parikh, March 2021</div>
					</textarea>
				</section>
			<section data-markdown><textarea data-template>
					## Topics
					* Ethereum Smart Contract Storage
					* Debugging Ethereum transactions
					* `try/catch` in Solidity
					* Custom errors in Solidity

				</textarea></section>

				<section>
					<h3>Smart contract Storage</h3>
					<ul>
						<li>This is where state variables are stored</li>
						<li>It is persistent between function calls and transactions</li>
						<li>It is a key-value mapping of 2<sup>256</sup> keys; each value is 32 bytes long</li>
					</ul>
				</section>

				<section>
					<h3>State Variables Storage Layout</h3>
					<ul>
						<li>One Astronomically large array</li>
						<li>Solidity generates code that saves variable values in their declaration order.</li>
						<li>Variable <span class="highlight">first</span> was declared first, itâ€™s stored in slot 0</li>
                                  		<li>Items that need less than 32 bytes are packed together. <a href="https://docs.soliditylang.org/en/latest/internals/layout_in_storage.html#layout-of-state-variables-in-storage" target="_blank"> See Rules</a></li>
					</ul>
					<pre><code data-trim>
						contract Sample {
							uint first;    // storage slot 0
							uint second;   // storage slot 1
						}
					</code></pre>
				</section>

				<section>
					<h3>Astronomically large array mental model</h3>
					<img src="storage.png">
				</section>

				<section data-markdown><textarea data-template>
					### Fixed-Sized Variables storage layout
					```js
					contract StorageTest {
						uint256 a;	// slot 0. (location = slot)
						uint256[2] b; // slots 1,2
					
						struct Entry { 
							uint256 id; // slot 3
							uint256 value; // slot 4
						}
						Entry c; // slots 3,4
					}
					```
					<img src="fixed.png" width=400>
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Dynamic-Sized Variables storage layout
					```js
					contract StorageTest {
						uint256 a;     // slot 0
						uint256[2] b;  // slots 1-2
					
						struct Entry {
							uint256 id;
							uint256 value;
						}
						Entry c;       // slots 3-4
						Entry[] d;
					}
					```
					<img src="dynamic.png" width=400>
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Dynamic-Sized Variables storage layout
					* Computing the location of an element of a dynamically-sized array:
					```js
					function arrLocation(uint256 slot, uint256 index, uint256 elementSize) 
					public pure returns (uint256) {
						return uint256(keccak256(slot)) + (index * elementSize);
					}
					```
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Mapping storage layout
					```js
					contract StorageTest {
						uint256 a;     // slot 0
						uint256[2] b;  // slots 1-2
					
						struct Entry {
							uint256 id;
							uint256 value;
						}
						Entry c;       // slots 3-4
						Entry[] d;     // slot 5 for length, keccak256(5)+ for data
					
						mapping(uint256 => uint256) e;
						mapping(uint256 => uint256) f;
					}
					```
					<img src="mapping.png" width=400>
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Mapping storage layout
					* Computing the location of a value given slot and key:
					```js
					function mapLocation(uint256 slot, uint256 key) 
					public pure returns (uint256) {
						return uint256(keccak256(key, slot));
					}
					```
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Complex types storage layout
					```js
					contract StorageTest {
						uint256 a;     // slot 0
						uint256[2] b;  // slots 1-2
						struct Entry {
							uint256 id;
							uint256 value;
						}
						Entry c;       // slots 3-4
						Entry[] d;     // slot 5 for length, keccak256(5)+ for data
					
						mapping(uint256 => uint256) e;    // slot 6, data at h(k . 6)
						mapping(uint256 => uint256) f;    // slot 7, data at h(k . 7)
						mapping(uint256 => uint256[]) g;  // slot 8
						mapping(uint256 => uint256)[] h;  // slot 9
					}
					```
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Complex types storage layout
					* To find `g[123][0]` :
					```js
					function mapLocation(uint256 slot, uint256 key) 
					public pure returns (uint256) {
						return uint256(keccak256(key, slot));
					}
					function arrLocation(uint256 slot, uint256 index, uint256 elementSize) 
					public pure returns (uint256) {
						return uint256(keccak256(slot)) + (index * elementSize);
					}
					// first find arr = g[123]
					arrLoc = mapLocation(8, 123);  // g is at slot 8
					// then find arr[0]
					itemLoc = arrLocation(arrLoc, 0, 1);
					```
                </textarea></section>

				<section data-markdown><textarea data-template>
					### Complex types storage layout
					* To find `h[2][456]` :
					```js
					function mapLocation(uint256 slot, uint256 key) 
					public pure returns (uint256) {
						return uint256(keccak256(key, slot));
					}
					function arrLocation(uint256 slot, uint256 index, uint256 elementSize) 
					public pure returns (uint256) {
						return uint256(keccak256(slot)) + (index * elementSize);
					}
					// first find map = h[2]
					mapLoc = arrLocation(9, 2, 1);  // h is at slot 9
					// then find map[456]
					itemLoc = mapLocation(mapLoc, 456);
					```
                </textarea></section>
                
				<section data-markdown><textarea data-template>
					### Packed Storage
					![Storage Layout](./storage.svg "Storage Layout")
                </textarea></section>
				
				<section data-markdown><textarea data-template>
					### Storage Demo
					- <a href="https://github.com/GeorgeBrownCollege-Toronto/Advanced-Smart-Contracts/tree/master/notes/storage-exception-debugging/demo/storage" target="_blank">Sample Contract</a>
					- call the delegator.setVersion() with value 0xaabbcc
					- check the values of val1,val2,val3,val4
					- use storageAt() to get the storage value
					- <a href="https://medium.com/aigang-network/how-to-read-ethereum-contract-storage-44252c8af925" target="_blank">How to read Ethereum contract storage</a>

                </textarea></section>

				<section>
					<h3>Lab</h3>
					Follow the instructions on the readme file of <a href="https://github.com/GeorgeBrownCollege-Toronto/Advanced-Smart-Contracts/tree/master/notes/storage-exception-debugging/lab/proxy-delegate" target="_blank">proxy-delegate</a>
				</section>

				<section data-markdown><textarea data-template>
				### Old way to `try/catch`
				```
				function execute(uint256 amount) external {
					// the low level call will return `false` if its execution reverts
					(bool success, bytes memory returnData) = address(this).call(
						abi.encodeWithSignature("onlyEven(uint256)",amount)
					);
					if (success) { /*handle success*/ } 
					else { /*handle exception*/ }
				}
			
				function onlyEven(uint256 a) public {
					// Code that can revert
					require(a % 2 == 0, "Ups! Reverting");
					// ...
				}
				```
				* Changes applied before/after the call are not rolled back here. 
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch` (1)
				* Handle external call failures
				* React on failed external calls and contract creation.
				* Cannot be used for internal function calls
				* Call public function with <span class="highlightRed">`this`</span> to make it external.
				* Eventual `out of gas` error is caught by the low-level `catch` clause. 
				* `out of gas` error is not caught if transaction executing code runs out of gas.
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(2)
				* following revert inside the try success block will revert the transaction:
				```
				function execute() public {
					try externalContract.someFunction() {
						// this will revert the execution of the transaction
						// even if the external call succeeded
						revert();
					} catch {
					   // ...
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(3)
				* Constructor calls
				```
				contract TryCatcher {
					// ...
					function execute() public {
						try new CalledContract() returns(CalledContract returnedInstance) {
							// returnedInstance can be used to obtain the 
							// address of the newly deployed contract
							emit SuccessEvent();
						} catch {
							emit CatchEvent();
						}
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(4)
				* External calls
				```
				contract CalledContract {    
					function getTwo() public returns (uint256) {
						return 2;
					}
				}
				contract TryCatcher {
					CalledContract public externalContract;
					function execute() public returns (uint256, bool) {
						try externalContract.getTwo() returns (uint256 v) {
							uint256 newValue = v + 2;
							return (newValue, true);
						} catch {
							emit CatchEvent();
						}		
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(5)
				* Use returned data inside catch statement
				```
				contract TryCatcher {
					event ReturnDataEvent(bytes someData);
					// ...
					function execute() public returns (uint256, bool) {
						try externalContract.someFunction() {
							// ...
						} catch (bytes memory returnData) {            
							emit ReturnDataEvent(returnData);
						}
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(6)
				* revert-style exception (`require` or `revert`) error signature matches <span class="highlightRed">`catch Error`</span> block.
				<pre class="hljs">
				contract TryCatcher {
					event ReturnDataEvent(bytes someData);
					event CatchStringEvent(string someString);
					event SuccessEvent();
					// ...
					function execute() public {
						try externalContract.someFunction() {
							emit SuccessEvent();
						} <span class="highlightRed">catch Error(string memory revertReason) {
							emit CatchStringEvent(revertReason);
						}</span> catch (bytes memory returnData) {
							emit ReturnDataEvent(returnData);
						}
					}
				}
			    </pre>
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(7)
				* assert-style exception (`assert`) error signature  matches <span class="highlightRed">`catch`</span> block
				<pre class="hljs">
				contract TryCatcher {
					event ReturnDataEvent(bytes someData);
					event CatchStringEvent(string someString);
					event SuccessEvent();
					// ...
					function execute() public {
						try externalContract.someFunction() {
							emit SuccessEvent();
						} catch Error(string memory revertReason) {
							emit CatchStringEvent(revertReason);
						} <span class="highlightRed">catch (bytes memory returnData) {
							emit ReturnDataEvent(returnData);
						}</span>
					}
				}
				</pre>
				</textarea></section>

				<section data-markdown><textarea data-template>
				### `try/catch`(8)
				* error like overflow/underflow matches <span class="highlightRed">`catch Panic()`</span> block
				<pre class="hljs">
				contract TryCatcher {
					event PanicErrorEvent(uint errorCode);
					event CatchStringEvent(string someString);
					event SuccessEvent();
					// ...
					function execute() public {
						try externalContract.someFunction() {
							emit SuccessEvent();
						} catch Error(string memory revertReason) {
							emit CatchStringEvent(revertReason);
						} <span class="highlightRed">catch Panic(uint errorCode) {
							emit PanicErrorEvent(errorCode);
						}</span>
					}
				}
				</pre>
				</textarea></section>


				<section data-markdown><textarea data-template>
				### `try/catch`(Panic codes)
				1. **`0x01`**: assert evaluates to false.
				2. **`0x11`**: underflow/overflow.
				3. **`0x12`**: e.g. `5/0` or `23%0`.
				4. **`0x21`**: Converting a value that is too big or negative into an enum type.
				5. **`0x31`**: Call `.pop()` on an empty array.
				6. **`0x32`**: bytesN or an array slice at an out-of-bounds or negative index (i.e. x[i] where i >= x.length or i < 0).
				7. **`0x41`**: too much memory or create large array.
				8. **`0x51`**: zero-initialized variable of internal function type.
				</textarea></section>

				<section data-markdown><textarea data-template>
				### Truffle debugger
				* Not debugging code in real-time.
				* Step over the historical execution of that transaction.
				* Mapping that execution onto its associated code. 
				* Debug any transaction given all artifacts, code & ABI. 
				```
				truffle debug \transaction hash\
				```

				</textarea></section>


				<section data-markdown><textarea data-template>
				### Debugging Error (An Infinite loop)
				```
				function set(uint x) public {
					while(true) {
					  myVariable = x;
					}
				}
				```
				<pre class="hljs">
					Error: VM Exception while processing transaction: out of gas
				</pre>
				</textarea></section>

				<section data-markdown><textarea data-template>
				### Debugging Error (An Infinite Error check)
				```
				function set(uint x) public {
					assert(x == 0);
					myVariable = x;
				}
				```
				<pre class="hljs">
					Error: VM Exception while processing transaction: invalid opcode
				</pre>
				</textarea></section>

				<section data-markdown><textarea data-template>
				### Debugging Error (A function isn't operating as desired)
				```
				event Odd();
				event Even();
				function set(uint x) public {
					myVariable = x;
					if (x % 2 == 0) {
						emit Odd();
					} else {
						emit Even();
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				### Custom errors in solidity
				* Older version : 
					* `revert("Insufficient funds.")`
				* Cons	
					* Expensive deploy cost
					* Difficult to use dynamic information
				* `error` (`v0.8.4` & above) statement can be declared inside/outside of smart contracts
				* [Issue](https://github.com/ethereum/solidity/issues/11278) in `try/catch` for custom error
				</textarea></section>

				<section data-markdown><textarea data-template>
				### Custom errors in solidity : Example 1
				```
				// SPDX-License-Identifier: GPL-3.0
				pragma solidity ^0.8.4;

				error Unauthorized();

				contract VendingMachine {
					address payable owner = payable(msg.sender);

					function withdraw() public {
						if (msg.sender != owner)
							revert Unauthorized();

						owner.transfer(address(this).balance);
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				### Custom errors in solidity : Example 2
				```
				// SPDX-License-Identifier: GPL-3.0
				pragma solidity ^0.8.4;

				/// Insufficient balance for transfer. Needed `required` but only
				/// `available` available.
				/// @param available balance available.
				/// @param required requested amount to transfer.
				error InsufficientBalance(uint256 available, uint256 required);

				contract TestToken {
					mapping(address => uint) balance;
					function transfer(address to, uint256 amount) public {
						if (amount > balance[msg.sender])
							// Error call using named parameters. Equivalent to
							// revert InsufficientBalance(balance[msg.sender], amount);
							revert InsufficientBalance({
								available: balance[msg.sender],
								required: amount
							});
						balance[msg.sender] -= amount;
						balance[to] += amount;
					}
				}
				```
				</textarea></section>

				<section data-markdown><textarea data-template>
				## References

				* [Solidity 0.8.4 Release Announcement](https://blog.soliditylang.org/2021/04/21/solidity-0.8.4-release-announcement/)
				* [Custom errors in Solidity](https://blog.soliditylang.org/2021/04/21/custom-errors/)
				* [Debugging a smart contract using truffle](https://www.trufflesuite.com/guides/debugging-a-smart-contract)
				* [Debugging a smart contract using hardhat and tenderly](http://blog.tenderly.co/level-up-your-smart-contract-productivity-using-hardhat-and-tenderly/)
				* [Understanding Ethereum contract storage](https://programtheblockchain.com/posts/2018/03/09/understanding-ethereum-smart-contract-storage/)
					
				</textarea></section>

				<section data-markdown><textarea data-template>

					## End
					
				</textarea></section>
			</div>
		</div>

		<script src="../../../reveal.js/dist/reveal.js"></script>
		<script src="../../../reveal.js/plugin/zoom/zoom.js"></script>
		<script src="../../../reveal.js/plugin/notes/notes.js"></script>
		<script src="../../../reveal.js/plugin/search/search.js"></script>
		<script src="../../../reveal.js/plugin/markdown/markdown.js"></script>
		<script src="../../../reveal.js/plugin/highlight/highlight.js"></script>
		<script>

			// Also available as an ES module, see:
			// https://revealjs.com/initialization/
			Reveal.initialize({
				controls: true,
				progress: true,
				center: true,
				hash: true,
				history: true,

				transition: 'none', // none/fade/slide/convex/concave/zoom

				// Learn about plugins: https://revealjs.com/plugins/
				plugins: [ RevealZoom, RevealNotes, RevealSearch, RevealMarkdown, RevealHighlight ]
			});

		</script>
	</body>
</html>
